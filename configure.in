dnl Process this file with autoconf to produce a configure script.
AC_REVISION($Revision: 553 $)
AC_PREREQ(2.0)
AC_INIT(src/alias-wrapper.c)


# Set VERSION so we only need to edit it in one place
AC_SUBST(VERSION)
VERSION=1.0b5


# /home/mailman is the default installation directory
AC_PREFIX_DEFAULT(/home/mailman)


# Check for Python!  Better be found on $PATH
AC_MSG_CHECKING(for --with-python)
AC_ARG_WITH(python, [
	--with-python         specify path to Python interpreter])
AC_MSG_RESULT($with_python)

if test "$with_python" = ""
then
	AC_PATH_PROG(with_python, python, /usr/local/bin/python)
fi

AC_MSG_CHECKING(Python interpreter)
if test ! -x $with_python
then
    AC_MSG_ERROR([

***** No Python interpreter found!
***** Try including the configure option
***** --with-python=/path/to/python/interpreter])
fi
AC_SUBST(PYTHON)
PYTHON=$with_python
AC_MSG_RESULT($PYTHON)

# Checks for programs.
AC_PROG_INSTALL
AC_PROG_MAKE_SET

# Find compiler, allow alternatives to gcc
AC_MSG_CHECKING(for --without-gcc)
AC_ARG_WITH(gcc, [--without-gcc             never use gcc], [
	case $withval in
	no)	CC=cc
		without_gcc=yes;;
	yes)	CC=gcc
		without_gcc=no;;
	*)	CC=$withval
		without_gcc=$withval;;
	esac], without_gcc=no;)
AC_MSG_RESULT($without_gcc)

# If the user switches compilers, we can't believe the cache
if test ! -z "$ac_cv_prog_CC" -a ! -z "$CC" -a "$CC" != "$ac_cv_prog_CC"
then
  AC_ERROR(cached CC is different -- throw away $cache_file
(it is also a good idea to do 'make clean' before compiling))
fi

AC_PROG_CC


# Optimizer/debugger flags passed between Makefiles
AC_SUBST(OPT)
if test -z "$OPT"
then
	case $GCC in
	yes)
		case $ac_cv_prog_cc_g in
		yes)	OPT="-g -O2";;
		*)	OPT="-O2";;
		esac
		;;
	*)	OPT="-O";;
	esac
fi

# We better be able to execute interpreters
AC_SYS_INTERPRETER
if test "$ac_cv_sys_interpreter" != "yes"
then
    AC_MSG_ERROR([

***** Cannot execute interpreter scripts?
***** Are you sure you system doesn't support this?])
fi


# new macros for finding UIDs
AC_DEFUN(MM_FIND_USER_ID, [
# $1 == variable name
# $2 == user id to check for
# $3 == purpose
AC_MSG_CHECKING(for $3 UID)
AC_SUBST($1)
changequote(,)
if test -z "$$1"
then
    cat > conftest.py <<EOF
import pwd
try:
    uid = pwd.getpwnam("$2")[2]
except KeyError:
    uid = ''
fp = open("conftest.out", "w")
fp.write("%s\n" % uid)
fp.close()
EOF
    python conftest.py
    $1=`cat conftest.out`
fi
changequote([, ])
if test -z "$$1"
then
    AC_MSG_ERROR([

***** User $2 not found!  You must first
***** set up the $2 entry in /etc/passwd
***** or set the $1 shell variable on the
***** configure command line])
fi
AC_MSG_RESULT($$1)
rm -f conftest.out conftest.py])

# new macro for finding GIDs
AC_DEFUN(MM_FIND_GROUP_ID, [
# $1 == variable name
# $2 == user id to check for
# $3 == purpose
AC_MSG_CHECKING(for $3 GID)
AC_SUBST($1)
changequote(,)
if test -z "$$1"
then
    cat > conftest.py <<EOF
import grp
try:
    gid = grp.getgrnam("$2")[2]
except KeyError:
    gid = ''
fp = open("conftest.out", "w")
fp.write("%s\n" % gid)
fp.close()
EOF
    python conftest.py
    $1=`cat conftest.out`
fi
changequote([, ])
if test -z "$$1"
then
    AC_MSG_ERROR([

***** Group $2 not found!  You must first
***** set up the $2 entry in /etc/group
***** or set the $1 shell variable on the
***** configure command line])
fi
AC_MSG_RESULT($$1)
rm -f conftest.out conftest.py])


# Now find the UIDs and GIDs
MM_FIND_USER_ID(CGI_UID, nobody, cgi_wrapper)
MM_FIND_GROUP_ID(CGI_GID, nobody, cgi_wrapper)
MM_FIND_USER_ID(ALIAS_UID, mailman, alias_wrapper)
MM_FIND_GROUP_ID(ALIAS_GID, mail, alias_wrapper)
MM_FIND_USER_ID(MAIL_UID, daemon, mail_wrapper)
MM_FIND_GROUP_ID(MAIL_GID, mail, mail_wrapper)


# Checks for libraries.

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(syslog.h)

# Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_UID_T
AC_TYPE_GETGROUPS

# Checks for library functions.
AC_FUNC_VPRINTF

dnl Output everything
AC_OUTPUT([misc/paths.py modules/mm_defaults.py
           src/Makefile misc/Makefile bin/Makefile modules/Makefile
	   mail/Makefile templates/Makefile cgi/Makefile cron/Makefile
	   filters/Makefile scripts/Makefile Makefile])
