# Copyright (C) 1998 by the Free Software Foundation, Inc.
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software 
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.

dnl Process this file with autoconf to produce a configure script.
AC_REVISION($Revision: 704 $)
AC_PREREQ(2.0)
AC_INIT(src/alias-wrapper.c)


# Set VERSION so we only need to edit it in one place
AC_SUBST(VERSION)
VERSION=1.0b5


# /home/mailman is the default installation directory
AC_PREFIX_DEFAULT(/home/mailman)


# Check for Python!  Better be found on $PATH
AC_MSG_CHECKING(for --with-python)
AC_ARG_WITH(python, [
	--with-python         specify path to Python interpreter])
AC_MSG_RESULT($with_python)

if test -z "$with_python"
then
	AC_PATH_PROG(with_python, python, /usr/local/bin/python)
fi

AC_MSG_CHECKING(Python interpreter)
if test ! -x $with_python
then
    AC_MSG_ERROR([

***** No Python interpreter found!
***** Try including the configure option
***** --with-python=/path/to/python/interpreter])
fi
AC_SUBST(PYTHON)
PYTHON=$with_python
AC_MSG_RESULT($PYTHON)

# Checks for programs.
AC_PROG_INSTALL
AC_PROG_MAKE_SET

# Find compiler, allow alternatives to gcc
AC_MSG_CHECKING(for --without-gcc)
AC_ARG_WITH(gcc, [--without-gcc             never use gcc], [
	case $withval in
	no)	CC=cc
		without_gcc=yes;;
	yes)	CC=gcc
		without_gcc=no;;
	*)	CC=$withval
		without_gcc=$withval;;
	esac], without_gcc=no;)
AC_MSG_RESULT($without_gcc)

# If the user switches compilers, we can't believe the cache
if test ! -z "$ac_cv_prog_CC" -a ! -z "$CC" -a "$CC" != "$ac_cv_prog_CC"
then
  AC_ERROR(cached CC is different -- throw away $cache_file
(it is also a good idea to do 'make clean' before compiling))
fi

AC_PROG_CC


# Optimizer/debugger flags passed between Makefiles
AC_SUBST(OPT)
if test -z "$OPT"
then
	case $GCC in
	yes)
		case $ac_cv_prog_cc_g in
		yes)	OPT="-g -O2";;
		*)	OPT="-O2";;
		esac
		;;
	*)	OPT="-O";;
	esac
fi

# We better be able to execute interpreters
AC_SYS_INTERPRETER
if test "$ac_cv_sys_interpreter" != "yes"
then
    AC_MSG_ERROR([

***** Cannot execute interpreter scripts?
***** Are you sure you system doesn't support this?])
fi


# new macro for finding GIDs
AC_DEFUN(MM_FIND_GROUP_ID, [
# $1 == variable name
# $2 == user id to check for
# $3 == purpose
AC_MSG_CHECKING(for $3 GID)
AC_SUBST($1)
changequote(,)
if test -z "$$1"
then
    cat > conftest.py <<EOF
import grp, string
gid = ''
for group in string.split("$2"):
    try:
        try:
	    gid = grp.getgrgid(int(group))
	    break
	except ValueError:
	    gid = grp.getgrnam(group)[2]
	    break
    except KeyError:
        gid = ''
fp = open("conftest.out", "w")
fp.write("%s\n" % gid)
fp.close()
EOF
    python conftest.py
    $1=`cat conftest.out`
fi
changequote([, ])
if test -z "$$1"
then
    AC_MSG_ERROR([

***** Group $2 not found!  You must first
***** set up the $2 entry in /etc/group
***** or set the $1 shell variable on the
***** configure command line])
fi
AC_MSG_RESULT($$1)
rm -f conftest.out conftest.py])

# Now find the UIDs and GIDs
# Support --with-mail-gid and --with-cgi-gid
AC_MSG_CHECKING(for --with-mail-gid)
AC_ARG_WITH(mail-gid, [
	--with-mail-gid  	specify GID mail programs run as])
if test -z "$with_mail_gid"
then
    with_mail_gid="other mail daemon"
fi
AC_MSG_RESULT($with_mail_gid)
MM_FIND_GROUP_ID(MAIL_GID, $with_mail_gid, mail_wrapper)

AC_MSG_CHECKING(for --with-cgi-gid)
AC_ARG_WITH(cgi-gid, [
	--with-cgi-gid  	specify GID CGI programs run as])
if test -z "$with_cgi_gid"
then
    with_cgi_gid="www www-data nobody"
fi
AC_MSG_RESULT($with_cgi_gid)
MM_FIND_GROUP_ID(CGI_GID, $with_cgi_gid, cgi_wrapper)

#MM_FIND_USER_ID(ALIAS_UID, mailman, alias_wrapper)
#MM_FIND_GROUP_ID(ALIAS_GID, mail, alias_wrapper)


# figure out the DEFAULT_HOST_NAME and DEFAULT_URL
AC_SUBST(FQDN)
AC_SUBST(URL)
changequote(,)
cat > conftest.py <<EOF
# attempt to figure out the default hostname and URL
from socket import *
import string
fqdn = None
www = None
host, aliases, ipaddrs = gethostbyaddr(gethostbyname(gethostname()))
aliases.insert(0, host)
for h in aliases:
    parts = string.split(h, '.')
    if len(parts) > 1:
        if parts[0] == 'www':
            www = h
        elif not fqdn:
            fqdn = h
fp = open('conftest.out', 'w')
if not www and fqdn:
    fp.write('%s\n%s\n' % (fqdn, fqdn))
elif www:
    dhn = string.join(string.split(www, '.')[1:], '.')
    fp.write('%s\n%s\n' % (dhn, www))
else:
    fp.write('please.change.me\nwww.please.change.me\n')
fp.close()
EOF
python conftest.py
changequote([, ])
AC_MSG_CHECKING(for default fully qualified host name)
if test -z "$FQDN"
then
    FQDN=`head -1 conftest.out`
fi
AC_MSG_RESULT($FQDN)
AC_MSG_CHECKING(for default URL host component)
if test -z "$URL"
then
    URL=`tail -1 conftest.out`
fi
AC_MSG_RESULT($URL)
rm -f conftest.out conftest.py

# Checks for libraries.

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(syslog.h)

# Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_UID_T
AC_TYPE_GETGROUPS

# Checks for library functions.
AC_FUNC_VPRINTF

dnl Output everything
AC_OUTPUT([misc/paths.py modules/mm_defaults.py
           src/Makefile misc/Makefile bin/Makefile modules/Makefile
	   mail/Makefile templates/Makefile cgi/Makefile cron/Makefile
	   filters/Makefile scripts/Makefile modules/mm_cfg.py
	   cron/crontab.in Makefile])
